Program:
Program2

Program2:
DeclList $

DeclList:
Decl
DeclList Decl

Decl:
FieldDecl
ClassDecl
MethodDecl
InterfaceDecl

FieldDecl:
TypeAndIdentifier ;

TypeAndIdentifier:
Type Identifier

Type:
Type [ ]
BasicType
ReferenceType

BasicType:
int
bool
double
string

ReferenceType:
_id

MethodDecl:
Prototype Block

Prototype:
TypeAndIdentifier ( ArgumentListOpt
void Identifier ( ArgumentListOpt

ArgumentListOpt:
)
ArgumentList )

ArgumentList:
TypeAndIdentifier
ArgumentList , TypeAndIdentifier

Block:
{ FieldDeclListOpt

FieldDeclListOpt:
StatementListOpt
FieldDeclList StatementListOpt

FieldDeclList:
FieldDecl
FieldDeclList FieldDecl

StatementListOpt:
}
StatementList }

StatementList:
Statement
StatementList Statement

Statement:
;
Block
break ;
IfStatement
Expression ;
ForStatement
WhileStatement
return ReturnExpressionOpt
println ( ExpressionList ) ;

IfStatement:
if ParExpression Statement
if ParExpression Statement else Statement

ParExpression:
( Expression )

ForStatement:
for ( ForControl Statement

ForControl:
ForControl1
Expression ForControl1

ForControl1:
; Expression ; )
; Expression ; Expression )

WhileStatement:
while ParExpression Statement

ReturnExpressionOpt:
;
Expression ;

ExpressionList:
Expression
ExpressionList , Expression

ClassDecl:
class _id Super

Super:
Interfaces
extends _id Interfaces

Interfaces:
{ ClassBodyOpt
implements IdentifierList { ClassBodyOpt

IdentifierList:
ReferenceType
IdentifierList , ReferenceType

ClassBodyOpt:
}
MemberList }

MemberList:
Member
MemberList Member

Member:
FieldDecl
MethodDecl

InterfaceDecl:
interface _id { PrototypeListOpt

PrototypeListOpt:
}
PrototypeList }

PrototypeList:
PrototypeDecl
PrototypeList PrototypeDecl

PrototypeDecl:
Prototype ;

Expression:
LValue = Expression
BinaryExpression

BinaryExpression:
BinaryExpression InfixOperator UnaryExpression
UnaryExpression

UnaryExpression:
PrefixOperator UnaryExpression
Primary

Primary:
LValue
Literal
MethodCall
ParExpression
readln ( )
newarray ( _integerliteral , Type )

PrefixOperator:
!
-

InfixOperator:
||
&&
==
!=
<
>
<=
>=
+
-
*
/

LValue:
Identifier
LValue [ Expression ]
LValue . Identifier

Identifier:
ReferenceType

Literal:
_doubleliteral
_stringliteral
_integerliteral
_booleanliteral

MethodCall:
MethodCall2
Identifier . MethodCall2

MethodCall2:
Identifier ( )
Identifier ( ExpressionList )
