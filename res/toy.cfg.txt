Program:
DeclList

DeclList:
Decl
DeclList Decl

Decl:
FieldDecl
ClassDecl
MethodDecl
InterfaceDecl

FieldDecl:
TypeAndIdentifier ;

TypeAndIdentifier:
Type _id

Type:
Type [ ]
BasicType
ReferenceType

BasicType:
int
bool
double
string

ReferenceType:
_id

MethodDecl:
Prototype Block

Prototype:
TypeAndIdentifier ( ArgumentListOpt
void ReferenceType ( ArgumentListOpt

ArgumentListOpt:
)
ArgumentList )

ArgumentList:
TypeAndIdentifier
ArgumentList , TypeAndIdentifier

Block:
{ FieldDeclListOpt

FieldDeclListOpt:
StatementListOpt
FieldDeclList StatementListOpt

FieldDeclList:
FieldDecl
FieldDeclList FieldDecl

StatementListOpt:
}
StatementList }

StatementList:
Statement
StatementList Statement

Statement:
;
Block
break ;
IfStatement
Expression ;
ForStatement
WhileStatement
return ReturnExpressionOpt
println ( ExpressionList ) ;

IfStatement:
if ParExpression Statement
if ParExpression Statement else Statement

ParExpression:
( Expression )

ForStatement:
for ( ForControl Statement

ForControl:
ForControl1
Expression ForControl1

ForControl1:
; Expression ; )
; Expression ; Expression )

WhileStatement:
while ParExpression Statement

ReturnExpressionOpt:
;
Expression ;

ExpressionList:
Expression
ExpressionList , Expression

ClassDecl:
class ReferenceType Super

Super:
Interfaces
extends ReferenceType Interfaces

Interfaces:
{ ClassBodyOpt
implements IdentifierList { ClassBodyOpt

IdentifierList:
ReferenceType
IdentifierList , ReferenceType


ClassBodyOpt:
}
MemberList }

MemberList:
Member
MemberList Member

Member:
FieldDecl
MethodDecl

InterfaceDecl:
interface ReferenceType { PrototypeListOpt

PrototypeListOpt:
}
PrototypeList }

PrototypeList:
PrototypeDecl
PrototypeList PrototypeDecl

PrototypeDecl:
Prototype ;

Expression:
LValue = BinaryExpression
BinaryExpression

BinaryExpression:
BinaryExpression InfixOperator UnaryExpression
UnaryExpression

UnaryExpression:
PrefixOperator UnaryExpression
LValue
Literal
MethodCall
ParExpression
readln ( )
newarray ( _integerliteral , Type )

PrefixOperator:
!
-

InfixOperator:
||
&&
==
!=
<
>
<=
>=
+
-
*
/

LValue:
ReferenceType
LValue [ Expression ]
LValue . ReferenceType

Literal:
_doubleliteral
_stringliteral
_integerliteral
_booleanliteral

MethodCall:
ReferenceType ( ExpressionListOpt
ReferenceType . ReferenceType ( ExpressionListOpt

ExpressionListOpt:
)
ExpressionList )